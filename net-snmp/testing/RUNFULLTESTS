#!/usr/bin/perl

use Getopt::Long;
use strict;

my %opts = ('g' => 'default');
GetOptions(\%opts, 
	   "v",
	   "h",
	   "q",
	   "g=s") || usage();
usage() if ($opts{'h'});

my %args = (
	    verbosity => ($opts{'v'} ? 1 : 0),
	    exec => \&decide_exec,
	    failures => ($opts{'q'} ? 0 : 1),
	    errors => ($opts{'q'} ? 0 : 1),
	   );

# create the testing harness infrastructure
use TAP::Harness;
my $harness = TAP::Harness->new(\%args);

# gather the tests
my @tests = gather_tests($opts{'g'});

# run the actual tests
$harness->runtests(@tests);

########################################
# decides how we should execute a test
#
sub decide_exec {
    my ( $harness, $test_file ) = @_;
    if ($test_file =~ /c$/) {
	return [$test_file];
    }
    return undef;
}

sub gather_tests {
    my ( $groups ) = @_;

    my @tests;

    foreach my $group (split(/,\s*/, $groups)) {
	my @files;

	print "checking group $group\n";

	# push on all files that start with T[NUM]*
	push_or_skip(\@tests, glob("fulltests/$group/T[0-9]*"));
    }

    return @tests;
}

sub push_or_skip {
    my ($array, @files) = @_;
    foreach my $file (@files) {
	next if ($file =~ /.(bak|old|orig|rej)$/);
	next if ($file =~ /~$/);
	push @$array, $file;
    }
}


# usage output
sub usage {
    print "$0 [-v] [-q] [-g GROUP]\n";
    print "  -v\t\tRuns in verbose mode; dumping all test output\n";
    print "  -q\t\tRuns in quieter mode; dumps less test output\n";
    print "  -g GROUP\tRuns the group of specified tests (default = 'default')\n";
    print "\t\t(use 'all' to run all tests)\n";
    print "  -h\t\tThis help output\n";
    exit;
}
