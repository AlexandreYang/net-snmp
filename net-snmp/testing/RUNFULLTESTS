#!/usr/bin/perl

use Getopt::Long;
use Data::Dumper;
use File::Path qw(make_path);
use strict;

########################################
# Globals and Command Line options

my %opts = ('g' => 'default',
	    'd' => 'fulltests',
	    'D' => '.');
GetOptions(\%opts, "v", "h", "q", "g=s", "r=s", "D=s", "debug") || usage();
usage() if ($opts{'h'});

# Build the harness object
my %args = (
	    verbosity => ($opts{'v'} ? 1 : 0),
	    exec => \&decide_exec,
	    failures => ($opts{'q'} ? 0 : 1),
	    errors => ($opts{'q'} ? 0 : 1),
	   );

# list of support infrastructure components
my %support;

# if the -d option was specified, pass on the source root directory to all apps
if (exists($opts{'d'})) {
    $ENV{'NETSNMPSRCDIR'} = $opts{'d'};
} else {
    $ENV{'NETSNMPSRCDIR'} = '.';
}

########################################
# Protection measures
$ENV{'SNMPCONFPATH'} = "/dev/null";

# create the testing harness infrastructure
use TAP::Harness;
my $harness = TAP::Harness->new(\%args);

########################################
# gather the tests

DEBUG("Gathering and building tests:\n"); 
find_support();
my @tests = gather_tests($opts{'g'}, $opts{'r'});

########################################
# rename them to remove parent dirs
@tests = rename_tests(@tests);

########################################
# run the tests

DEBUG("Running tests:\n"); 
DEBUG("-" x 78, "\n");

my $results = $harness->runtests(@tests);

if (!$opts{'q'}) {
    my @failed = $results->failed();
    print "\nWe failed these ", (1 + $#failed), " tests:\n";
    print "  ", join("\n  ",@failed), "\n";
}

exit($results->all_passed() ? 0 : 1);

######################################################################
# Infrastructure
#

########################################
# decides how we should execute a test
#
sub decide_exec {
    my ( $harness, $testfile ) = @_;

    # 1) if it's a known executable type, just run it.
    if ($testfile =~ /\.exe$/) {
	return [$testfile];
    }

    # 1) if it's a known executable built by libtool, call libtool to run it.
    if ($testfile =~ /\.exe\.lt$/) {
	return ["../libtool", "--mode=execute", $testfile];
    }

    # see if we have a registered runner script
    my ($dirname, $groupname, $basename, $app_extension, $file_extension) = 
      ($testfile =~ /([^\/]+)\/([^\/]+)\/([^\/]+)_([^_]+)\.*([^\.]*)$/);

    # 2) we have a BUILD_TYPE file in the same directory
    if (exists($support{'run'}{$app_extension}{$groupname})) {
	return [$support{'run'}{$app_extension}{$groupname}, $testfile];
    }

    # 3) return a generic build script
    if (exists($support{'run'}{$app_extension}{'generic'})) {
	return [$support{'run'}{$app_extension}{'generic'}, $testfile];
    }

    # 4) give up and let the test harness decide itself
    return undef;
}

sub gather_tests {
    my ( $groups, $regexp ) = @_;
    my @groups;

    # figure out the list of groups we need to search through
    if ($groups eq 'all') {
	# find every group we can
	# we exclude:
	#  - things not a directory
	#  - anything with "template" in the name
	@groups =
	  grep { !/(template|support)/ &&
		   -d $_ && s/$opts{'D'}\/$opts{'d'}.// } glob("$opts{'D'}/$opts{'d'}/*");
    } else {
	# they specified a comma separated list
	@groups = split(/,\s*/, $groups);
    }
    DEBUG("Checking groups: ", join(", ", @groups), "\n");

    my @tests;
    foreach my $group (@groups) {
	my @files;

	DEBUG("checking group $group\n");

	if (! -d "$opts{'D'}/$opts{'d'}/$group") {
	    ERROR("group '$group' is not a directory under '$opts{'D'}/$opts{'d'}'; ignoring\n");
	    next;
	}

	# push on all files that start with T[NUM]*
	push_or_skip(\@tests, $regexp, glob("$opts{'D'}/$opts{'d'}/$group/T[0-9]*"));
    }

    return @tests;
}

sub push_or_skip {
    my ($array, $regexp, @files) = @_;
    foreach my $file (@files) {
	next if ($file =~ /.(bak|old|orig|rej)$/);
	next if ($file =~ /~$/);
	next if (defined($regexp) && $file !~ /$regexp/);
	DEBUG("  Adding file $file\n");
	push @$array, build_test($file);
    }
}

# rename all the tests to remove the top subdir to help readability
sub rename_tests {
    my (@tests) = @_;
    my @newtests;

    # yes, I could have used map.  But I didn't.
    foreach my $file (@tests) {
	my $title = "$file";
	$title =~ s/$opts{'d'}.//;
	$title =~ s/^$opts{'D'}.//;
	push @newtests, [$file, $title];
    }
    return @newtests;
}

# called to possibly manipluate the list of tests to run by building some
sub build_tests {
    my (@tests) = @_;
    my @newtests;

    foreach my $test (@tests) {
	my $title;
	
	my $built = build_test($test);
	if (ref($built) eq 'ARRAY') {
	    push @newtests, @$built;
	} elsif ($built ne "") {
	    push @newtests, $built;
	}
    }
    return @newtests;
}

#
# Finds scripts that are used to build and run actual commands
#
sub find_builders {
    $support{'build'} = {};
    find_scripts('build', $support{'build'});
}

sub find_runners {
    $support{'run'} = {};
    find_scripts('run', $support{'run'});
}

sub find_support {
    find_builders();
    find_runners();
}

sub find_scripts {
    my ($buildname, $hashref) = @_;
    my $count;
    DEBUG("looking for $buildname scripts\n");
    foreach my $builder (glob("$opts{'D'}/$opts{'d'}/*/${buildname}_*")) {
	next if ($builder =~ /~$/);
	next if ($builder =~ /.(bak|orig|rej|old)$/);

	my ($group, $type) = ($builder =~ /([^\/]+)\/${buildname}_(.*)/);
	# save this as a certain group builder
	$hashref->{$type}{$group} = $builder;

	# save this as a generic builder if there isn't a better
	# generic one, such as one that exists in the support
	# directory.
	if (!exists($hashref->{$type}{'generic'}) || $group eq 'support') {
	    $hashref->{$type}{'generic'} = $builder;
	}
	$count++;
    }
    DEBUG("  found $count\n");
}

# called to build a test from a registerd builder
sub build_test {
    my ($testfile) = @_;

    my ($dirname, $groupname, $basename, $app_extension, $file_extension) = 
      ($testfile =~ /([^\/]+)\/([^\/]+)\/([^\/]+)_([^_]+)\.([^\.]+)$/);

    # is this even a buildable type recipe?
    if (!$dirname || !$basename || !$app_extension || !$file_extension) {
	return $testfile;
    }

    DEBUG("found: $testfile => $dirname, $basename, $app_extension, $file_extension\n");

    # choices:
    # 1) we have a registered subroutine to build an extension from
    # XXX

    # 2) we have a BUILD_TYPE file in the same directory
    if (exists($support{'build'}{$app_extension}{$dirname})) {
	return
	  call_build_script($support{'build'}{$app_extension}{$dirname}, $testfile);
    }

    # 3) return a generic build script
    if (exists($support{'build'}{$app_extension}{'generic'})) {
	return
	  call_build_script($support{'build'}{$app_extension}{'generic'}, $testfile);
    }

    # 4) we assume it's fine as is
    return $testfile;
}

sub call_build_script {
    my ($scriptname, $filename) = @_;

    my $maybenewfile = $filename;
    $maybenewfile =~ s/.[^\.]+$/.exe/;   # add .exe suffix
    $maybenewfile =~ s/T([^\/]+)$/B$1/;  # change prefix to B for 'Built'
    $maybenewfile =~ s/^$opts{'D'}\///;

    my $newpath = $maybenewfile;
    $newpath =~ s/\/[^\/]*$//;

    if (! -d $newpath) {
	DEBUG("making directory $newpath\n");
	make_path($newpath);
    }

    my $lastline;
    DEBUG("BUILDING: $scriptname $filename $maybenewfile\n");
    open(B,"$scriptname $filename $maybenewfile|");
    while (<B>) {
	$lastline = $_;
    }
    chomp($lastline);

    DEBUG("  result: $lastline\n");
    return undef if ($lastline eq 'fail');
    return undef if ($lastline eq 'skip');
    return $filename if ($lastline eq '');
    return $lastline;
}

# usage output
sub usage {
    print "$0 [-v] [-q] [-g GROUP]\n";
    print "  -v\t\tRuns in verbose mode; dumping all test output\n";
    print "  -q\t\tRuns in quieter mode; dumps less test output\n";
    print "  -g GROUP\tRuns the group of specified tests (default = 'default')\n";
    print "\t\t(use 'all' to run all tests)\n";
    print "  -r\t\tOnly run test files matching this regexp\n";
    print "  -h\t\tThis help output\n";
    print "  --debug\t\tDebugging output\n";
    exit;
}

sub DEBUG {
    return if (!$opts{'debug'});
    print @_;
}

sub ERROR {
    print STDERR "Error:", @_;
}
