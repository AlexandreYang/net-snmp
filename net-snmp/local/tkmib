#!/usr/local/bin/perl -w

require Tk::Table;
require Tk::HList;
require Tk::FileSelect;

$host = 'localhost';
$community = 'public';
$port = '161';
$OID = '.1.3.6.1';

$graphtime=5;
$graphdelta=1;
@graphcolors=qw(blue red green yellow purple);

@displayInfo=qw(type access status units hint enums moduleID);

use Tk;
require Tk::HList;
use SNMP;
$SNMP::save_descriptions=1;
$SNMP::use_long_names=1;
$SNMP::use_enums=1;
SNMP::initMib();

my $tmpbd = 3;

$top = MainWindow->new();
$top->title("tkmib");

#Menus
$MenuFrame = $top->Frame(-relief => "raised",-borderwidth => 2);
$MenuFrame->pack(-fill => "x",-expand => 1);
$FileMenuBut = $MenuFrame->Menubutton(-text => "File",
				      -menuitems =>
      [
#       [Button => "Save Output", -command => [\&saveOutput]],
       [Button => "Quit",        -command => [\&exit]]
       ])->pack(-side => 'left');

$MibMenuBut = $MenuFrame->Menubutton(-text => "Mib",
				      -menuitems =>
      [[Button => "Load a New Mib File", -command => [\&loadNewMibFile]],
       [Button => "Load a New Mib Module", -command => [\&loadNewMibModule]]
       ])->pack(-side => 'left');

$OptMenuBut = $MenuFrame->Menubutton(-text => "Options",
				     -menuitems =>
      [[Cascade => "~Display"]
       ])->pack(-side => 'left');
        
$tmp = $OptMenuBut->cget(-menu);
$OptMenuWidgets = $tmp->entrycget("Display", -menu);

# $OptMenuBut->cascade(-label => 'sub');
#$sub1 = $OptMenuBut->Optionmenu(-text => 'options', -options => ['hehe',["hi", "there"]]);
#$sub1->pack();
# $OptMenuBut->entryconfigure('sub', -menu => $sub1);

$hlist=$top->Scrolled(qw(HList -itemtype imagetext -browsecmd main::showInfo
			 -command main::showChildren -width 80 -height 15));
$hlist->pack(-side => 'top', -expand => 1, -fill => 'both');
my $sFrame = $top->Frame(-relief => 'raised', -borderwidth => $tmpbd);
$sFrame->pack(-side => 'top', -fill => 'x');
$sFrame->Label(-text => 'OID: ', -relief => 'raised', -borderwidth => $tmpbd)
    ->pack(-side => 'left');
$mibOID = $sFrame->Entry(-textvariable => \$OID, -relief => 'flat');
$mibOID->pack(-side => 'left');
$mibTextOID = $sFrame->Label(-text => '');
$mibTextOID->pack(-side => 'right');

$dispFrame=$top->Frame(-relief => 'raised', -borderwidth => $tmpbd);
#$dispFrame->pack(-side => 'top', -fill =>'x');
$dispFrame->pack(-side => 'top', -fill =>'x');
# my $f1 = $dispFrame->Frame(-relief => 'raised', -borderwidth => $tmpbd);
# $f1->pack(-side => 'left', -fill => 'y');
# my $f2 = $dispFrame->Frame();
# $f2->pack(-side => 'left', -fill => 'both');
for($i=0;$i<= $#displayInfo;$i++) {
    createRow($i);
    optionalWidget($i,$OptMenuWidgets);
}
    
my $tmpFrame=$top->Frame(-relief => 'raised', -borderwidth => $tmpbd);
$tmpFrame->pack(-side => 'top', -fill =>'x');
$tmpFrame->Label(-text => 'Description:', -anchor => 'w')->pack(-side => 'top',
								-fill => 'x');
$descr = $tmpFrame->Scrolled(qw(Text -width 80 -height 4));
$descr->pack(-side => 'top', -fill => 'x');

$bFrame = $top->Frame(-relief => 'raised', -borderwidth => $tmpbd);
$bFrame->pack(-side => 'top', -fill => 'x');
$hostEntry = $bFrame->Entry(-textvariable => \$host, -width => 12);
$hostEntry->pack(-side => 'left');
$commEntry = $bFrame->Entry(-textvariable => \$community, -width => 12);
$commEntry->pack(-side => 'left');
$portEntry = $bFrame->Entry(-textvariable => \$port, -width => 12);
$portEntry->pack(-side => 'left');
$bFrame->Button(-text => 'graph', -command => \&snmpgraph)->pack(-side => 'right');
$bFrame->Button(-text => 'table', -command => \&snmptable)->pack(-side => 'right');
$bFrame->Button(-text => 'walk', -command => \&snmpwalk)->pack(-side => 'right');
$bFrame->Button(-text => 'getnext', -command => \&snmpgetnext)->pack(-side => 'right');
$bFrame->Button(-text => 'get', -command => \&snmpget)->pack(-side => 'right');
$oFrame = $top->Frame(-borderwidth => $tmpbd, -relief => 'raised');
$oFrame->pack(-side => 'top', -fill => 'both');
$output = $oFrame->Scrolled(qw(Text -width 80 -height 14));
$output->pack(-side => 'top', -fill => 'both', -expand => 1);

$tmpFrame = $top->Frame(-relief => 'raised', -borderwidth => $tmpbd);
$tmpFrame->pack(-side => 'top', -fill => 'x');
$tmpFrame->Label(-text => "Status:  ", -anchor => 'w')
#		 -relief => 'raised', -borderwidth => $tmpbd)
    ->pack(-side => 'left');
$status = $tmpFrame->Label(-anchor => 'w');
$status->pack(-side => 'left', -fill => 'x');

# initialize the browser
foreach $i (qw(.1 .1.3 .1.3.6 .1.3.6.1)) {
    addMibOID($i);
}
showChildren("1.3.6.1");

MainLoop();

sub snmpsetup {
    my $oid = $OID;
    my $tag = SNMP::translateObj($oid);
    my $sess = new SNMP::Session(DestHost => $host, Community => $community,
				 RemotePort => $port,
				 UseLongNames => 1, Timeout => 5000000);
    my $var = new SNMP::Varbind([$oid]);
    initText();
    return ($oid, $sess, $var);
}

sub initText {
    if (ref($output) eq "Tk::Frame" && defined($$output{'_#text'})) {
	$output->delete('0.0','end');
    } else {
	$output->destroy();
	$output = $oFrame->Scrolled(qw(Text -width 80 -height 14));
	$output->pack(-side => 'top', -fill => 'both', -expand => 1);
    }
}

sub initTable {
    $output->destroy();
    $oFrame->packPropagate(0);
    $output = $oFrame->Table(-columns => shift, -width => 80, -height => 14,
			     -fixedrows => 2, -fixedcolumns => 2);
    $output->pack(-side => 'top', -fill => 'both', -expand => 1);
}

sub initCanvas {
    $output->destroy();
    $oFrame->packPropagate(0);
    $output = $oFrame->Scrolled(qw(Canvas -width 80c -height 14c));
    $output->pack(-side => 'top', -fill => 'both', -expand => 1);
}

sub snmpget {
    (my $oid, my $sess, my $var) = snmpsetup();
    $status->configure(-text => "getting:  $host $community $oid");
    $top->update();
    my $val = $sess->get($var);
    if ($sess->{ErrorStr}) {
	$status->configure(-text => $sess->{ErrorStr});
    } else {
	$output->insert('end',"$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f] = $var->[$SNMP::Varbind::val_f]\n");
	$status->configure(-text => "");
    }
}

sub snmpgetnext {
    (my $oid, my $sess, my $var) = snmpsetup();
    $status->configure(-text => "get next:  $host $community $oid");
    $top->update();
#    print "var: " . $var->tag . "\n";
    my $val = $sess->getnext($var);
#    print "var: " . $var->tag . "\n";
    if ($sess->{ErrorStr}) {
	$status->configure(-text => $sess->{ErrorStr});
    } else {
	$output->insert('end',"$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f] = $var->[$SNMP::Varbind::val_f]\n");
	$status->configure(-text => "");
    }
}

sub snmpwalk {
    (my $oid, my $sess, my $var) = snmpsetup();
    $status->configure(-text => "walking:  $host $community $oid");
    $top->update();
    while (!$sess->{ErrorStr}) {
	my $val = $sess->getnext($var);
	last if (!defined($var->tag) ||
		 $sess->{ErrorStr} ||
	         SNMP::translateObj($var->tag) !~ /^\.$oid/);
	$output->insert('end',"$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f] = $var->[$SNMP::Varbind::val_f]\n");
	$top->update();
    }
    if ($sess->{ErrorStr}) {
	$status->configure(-text => $sess->{ErrorStr});
	$output->insert('end',"$sess->{ErrorStr} ($sess->{ErrorNum})\n");
    } else {
	$status->configure(-text => "");
    }
}

sub snmptable {
    (my $oid, my $sess, my $var) = snmpsetup();
    $status->configure(-text => "collecting data:  $host $community $oid");
    $top->update();
    my (@tb, @tags, @index);
    while (!$sess->{ErrorStr}) {
	my $val = $sess->getnext($var);
	last if (!defined($var->tag) ||
		 $sess->{ErrorStr} ||
	         SNMP::translateObj($var->tag) !~ /^\.$oid/);
	$newoid = "$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f]";
	$output->insert('end',"$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f] = $var->[$SNMP::Varbind::val_f]\n");
	$top->update();
	$newoid =~ /([^\.]+)\.([0-9\.]+)$/;
	if (!grep(/$1/,@tags)) {
	    push @tags,$1;
	}
	if (!grep(/$2/,@index)) {
	    push @index,$2;
	}
	$tb{$2}{$1} = $var->val;
    }
    initTable($#tags+1);
    for(my $k=0;$k <= $#tags;$k++) {
	$output->put(1,$k+2,$tags[$k]);
    }
    $output->put(1,1,"Index");
    for(my $i=0;$i <= $#index;$i++) {
	$output->put($i+2,1,$index[$i]);
    }
    for(my $i=0;$i <= $#index; $i++) {
	for(my $k=0;$k <= $#tags;$k++) {
	    $output->put($i+2,$k+2,$tb{$index[$i]}{$tags[$k]});
	}
    }
    $status->configure(-text => "");
}

sub snmpgraph {
    ($graphoid, $graphsess, my $graphvar) = snmpsetup();
    $top->update();
    %graphtb = ();
    @graphvars = ();
    initCanvas();
    $gcount=0;
    $max=-1;
    $min=2**32-1;
    updateGraph();
    $output->repeat($graphtime*1000, \&updateGraph);
}

sub updateGraph() {
    $status->configure(-text => "collecting data:  $host $community $graphoid");
    my $oid = $graphoid;
    my $tag = SNMP::translateObj($graphoid,0);
    my $var = new SNMP::Varbind([$oid]);
    $graphsess->{ErrorStr} = "";    
    while (!$graphsess->{ErrorStr}) {
	my $val = $graphsess->getnext($var);
	last if ($graphsess->{ErrorStr} ||
		 !defined($var->tag) ||
	         SNMP::translateObj($var->tag) !~ /^\.$oid/);
	my $newoid = SNMP::translateObj("$var->[$SNMP::Varbind::tag_f].$var->[$SNMP::Varbind::iid_f]");
	$top->update();
	$newoid =~ /$oid\.([0-9\.]+)$/;
	if (defined($1)) {
	    if (!grep(/$1/,@graphvars)) {
		push @graphvars,$1;
		print "index:  $1\n";
	    }
	    if ($graphdelta) {
		if ($gcount > 0) {
		    $graphtb{$1}[$gcount-1] = $var->val - $prev{$1};
		}
		$prev{$1} = $var->val;
	    } else {
		$graphtb{$1}[$gcount] = $var->val;
	    }
	    $max = $graphtb{$1}[$#{$graphtb{$1}}] 
		if ($#{$graphtb{$1}} >= 0 &&
		    $graphtb{$1}[$#{$graphtb{$1}}] > $max);
	    $min = $graphtb{$1}[$#{$graphtb{$1}}] 
		if ($#{$graphtb{$1}} >= 0 &&
		    $graphtb{$1}[$#{$graphtb{$1}}] < $min);
	}
    }
    if ($gcount > 1) {
	$output->delete('all');
	my $canvas = $$output{'SubWidget'}{'canvas'};
	my $h=$canvas->cget(-height);
	foreach $i (@graphvars) {
	    my @a = ();
	    for(my $j=0; $j <= $#{$graphtb{$i}}; $j++) {
		$a[$j*2] = $j;
		$a[$j*2+1] = $h-(($h-3)*($graphtb{$i}[$j]-$min))/($max-$min)-3;
	    }
	    $output->createLine(@a, -fill => $graphcolors[$i%$#graphcolors]);
	}
    }
    $gcount++;
    $status->configure(-text => "sleeping for $graphtime seconds");
}

sub addMibOID {
    my $i = shift;
    $i = ".$i" if ($i !~ /^\./);
    my $name = SNMP::translateObj($i,1);
    if (defined($name)) {
	$name =~ s/.*\.([^.]+)$/$1/;
    } else {
	return;
    }
    $i =~ s/^\.//;
    $hlist->add($i, -text => $name);
}

sub showInfo {
    $OID = shift;
    my $oid = $OID;
    $mibOID->configure(-textvariable => \$OID);
    $mibOID->update();
    if (!defined($last) || "$last" ne $oid) {
	updateInfo($oid);
    }
    $last = $oid;
}
	
sub showChildren {
    $OID = shift;
    my $oid = $OID;
    $mibOID->configure(-textvariable => \$OID);
    if ($hlist->infoChildren($oid)) {
	my @a = $hlist->infoChildren($oid);
	my $i;
	foreach $i (@a) {
	    $hlist->deleteEntry($i);
	}
    } else {
	$oid = ".$oid";
	my $mib = $SNMP::MIB{$oid};
	if (defined($mib)) {
	    my $children = $$mib{'children'}; 
	    if (ref($children) eq "ARRAY") {
		foreach $i (sort {$$a{'subID'} <=> $$b{'subID'}} @{$children}) {
		    addMibOID($$i{'objectID'});
		}
	    } else {
		$status->configure(-text => SNMP::translateObj($oid,1) . 
				   " has no children");
		return;
	    }
	}
    }
    $status->configure(-text => "");
}

sub updateInfo {
    $OID = shift;
    my $oid = $OID;
    my $mib = $SNMP::MIB{".$oid"};
    if (defined($mib)) {
	$mibOID->configure(-text => $mib->{'objectID'});
	$mibTextOID->configure(-text => 
			     SNMP::translateObj($mib->{'objectID'},1));
	$descr->delete('0.0','end');
	if (defined($mib->{'description'}) && 
	    $mib->{'description'} ne "") {
	    my $desc = $mib->{'description'};
	    $desc =~ s/\n[ \t]+/\n/g;
	    $descr->insert('end',$desc);
	}
	for($i=0; $i<= $#displayInfo;$i++) {
	    $dpyInfo[$i] = $mib->{$displayInfo[$i]};
	}
    }
}

sub optionalWidget {
    my $num = shift;
    my $menu = shift;
    my $state = 1;
    $menu->checkbutton(-label => $displayInfo[$num], 
		       -variable => \$state,
		       -command => [\&toggleWidgetShown, $num]);
}

sub createRow {
    my $i = shift;
    $dispFrame->Label(-text => $displayInfo[$i], 
		     -anchor => 'w', -borderwidth => $tmpbd)
	->grid(-column => 0, -row => $i, -sticky => 'w');
    $dpyInfo[$i] = "" if (!defined($dpyInfo[$i]));
    my $ent = $dispFrame->Entry(-textvariable => \$dpyInfo[$i], -width => 80, 
		      -relief => 'flat', -borderwidth => $tmpbd);
    $ent->grid(-column => 1, -row => $i, -sticky => 'w');
}

sub toggleWidgetShown {
    my $self = shift;
    my @widgets = $dispFrame->gridSlaves(-row => $self);
    my $i;
    if ($#widgets >= 0) {
	for($i = 0; $i <= $#widgets; $i++) {
	    $widgets[$i]->gridForget;
	    $widgets[$i]->destroy();
	}
    } else {
	createRow($self);
    }
}

sub loadNewMibFile {
    my $sel = $top->FileSelect();
    my $file = $sel->Show();
    if (defined($file)) {
      SNMP::addMibFiles($file);
	showChildren("1.3.6.1");
	showChildren("1.3.6.1");
    }
}

sub loadNewMibModule {
    my $tmptop = MainWindow->new();
    my $var = "";
    $tmptop->Label(-text => "Enter a SNMP MIB module name")
	->pack(-side => 'top');
    my $e = $tmptop->Entry(-textvariable => \$var);
    $e->pack(-side => 'top');
    $e->bind('<Return>',[\&loadIt,\$var,$tmptop]);
    my $f = $tmptop->Frame();
    $f->pack(-side => 'top');
    $f->Button(-text => 'Ok', -command => [\&loadIt,"",\$var,$tmptop])
	->pack(-side => 'left');
    $f->Button(-text => 'Cancel', -command => [sub { my $wid = shift;
						     $wid->destroy(); },
					       $tmptop])
	->pack(-side => 'left');
}

sub loadIt {
    my $tmp = shift;
    my $var = shift;
    my $wid = shift;
    $wid->destroy();
    if ($$var ne "") {
        my $ret = SNMP::loadModules($$var);
	if ($ret) {
	    showChildren("1.3.6.1");
	    showChildren("1.3.6.1");
	} else {
	    $status->configure(-text => "Failed reading module $$var");
	}
    }
}
